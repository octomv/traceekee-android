apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: "com.cookpad.android.plugin.license-tools"
apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'com.jaredsburrows.license'
buildscript {
    repositories {
        google()
    }

    dependencies {
        classpath 'com.google.firebase:firebase-appdistribution-gradle:1.4.0'
    }
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "mv.gov.mohiru"
        resValue "string", "build_config_package", "mv.gov.mohiru"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 41
        versionName "1.0.41"
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }

        buildConfigField "String", "STORE_URL", "\"https://play.google.com/store/apps/details?id=mv.gov.mohiru\""
        buildConfigField "String", "PRIVACY_URL", "\"https://traceekee.netlify.app/privacy.html\""

        buildConfigField "String", "FIREBASE_REGION", "\"asia-east2\""

        buildConfigField "String", "ORG", "\"MV_CTO\""
        buildConfigField "int", "SERVICE_FOREGROUND_NOTIFICATION_ID", "960178"
        buildConfigField "String", "SERVICE_FOREGROUND_CHANNEL_ID", "\"TraceEkee Updates\""
        buildConfigField "String", "SERVICE_FOREGROUND_CHANNEL_NAME", "\"TraceEkee Foreground Service\""

        buildConfigField "String", "PUSH_NOTIFICATION_CHANNEL_NAME", "\"TraceEkee Notifications\""
        buildConfigField "int", "PUSH_NOTIFICATION_ID",     "960179"

        buildConfigField "long", "SCAN_DURATION",           "8000"
        buildConfigField "long", "MIN_SCAN_INTERVAL",       "36000"
        buildConfigField "long", "MAX_SCAN_INTERVAL",       "43000"
        buildConfigField "long", "MAX_QUEUE_TIME",          "7000"
        buildConfigField "long", "BM_CHECK_INTERVAL",       "540000"
        buildConfigField "long", "HEALTH_CHECK_INTERVAL",   "900000"
        buildConfigField "long", "PURGE_INTERVAL",          "86400000"
        buildConfigField "long", "PURGE_TTL",               "1814400000"
        buildConfigField "long", "CONNECTION_TIMEOUT",      "6000"
        buildConfigField "long", "BLACKLIST_DURATION",      "100000"

        buildConfigField "long", "ADVERTISING_DURATION",    "180000"
        buildConfigField "long", "ADVERTISING_INTERVAL",    "5000"

        buildConfigField "String", "V2_CHARACTERISTIC_ID", "\"5C2F325F-FC58-4235-8E9A-3B86FC969F1D\""

    }

    buildTypes {
        debug {
            buildConfigField "String", "FIREBASE_UPLOAD_BUCKET", "\"keen-bongo-274410upload\""
            buildConfigField "String", "BLE_SSID", "\"CC60F5D3-22D4-4F62-9D78-FAF2EE0ADBA2\""

            String ssid = "\"CC60F5D3-22D4-4F62-9D78-FAF2EE0ADBA2\""
            versionNameSuffix "-debug-${getGitHash()}-${ssid.substring(ssid.length() - 5, ssid.length() - 1)}"
            resValue "string", "app_name", "TraceEkee Debug"
            applicationIdSuffix ".stg"
        }

        //HpaApp$2019
        release {

            buildConfigField "String", "FIREBASE_UPLOAD_BUCKET", "\"covid19trace-upload\""
            buildConfigField "String", "BLE_SSID", "\"EC5C3477-98C9-48FC-8B3B-8FF71FCCD6C8\""

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled false
            multiDexEnabled false
            zipAlignEnabled true
            resValue "string", "app_name", "TraceEkee"

            lintOptions {
                // Ignore lint errors for now
                abortOnError false
            }
            signingConfig debug.signingConfig

        }
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }


    // Add for using latest experimental build of Android Extensions
    androidExtensions {
        experimental = true
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation group: 'androidx.localbroadcastmanager', name: 'localbroadcastmanager', version: '1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.firebase:firebase-config-ktx:19.1.3'
    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta04'
    implementation 'com.google.firebase:firebase-storage:19.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.hbb20:ccp:2.3.4'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Room components
    def room_version = "2.2.5"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // For Kotlin use kapt instead of annotationProcessor

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // optional - Guava support for Room, including Optional and ListenableFuture
    implementation "androidx.room:room-guava:$room_version"

    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation "com.google.android.material:material:1.2.0-alpha06"

    def archLifecycleVersion = "2.2.0"
    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:$archLifecycleVersion"
//    kapt "androidx.lifecycle:lifecycle-compiler:$archLifecycleVersion"
    implementation "android.arch.lifecycle:common-java8:$archLifecycleVersion"

    // ViewModel Kotlin support
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'

    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-auth:19.3.0'
    implementation 'com.google.firebase:firebase-functions:19.0.2'
    implementation 'com.google.firebase:firebase-messaging:20.1.5'

    //bottom navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.1'

    //cardview
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.guava:guava:28.2-android'

    //Lottie
    implementation 'com.airbnb.android:lottie:3.4.0'

}
